plugins {
    id 'java'
}

group = 'net.anawesomguy'
version = '1.0'

repositories {
    mavenCentral()
}

sourceSets {
    year2024
    year2025
}

configurations {
    yearsImplementation
}

dependencies {
    //implementation 'org.apache.commons:commons-collections4:4.5.0-M2'
    implementation 'org.jetbrains:annotations:26.0.+'
    implementation 'it.unimi.dsi:fastutil-core:8.5.+'
    implementation('com.google.guava:guava:33.5.0-jre') {
        exclude group: 'org.checkerframework', module: 'checker-qual'
        exclude group: 'com.google.guava', module: 'listenablefuture'
        exclude group: 'com.google.j2objc', module: 'j2objc-annotations'
    }

    yearsImplementation 'org.apache.commons:commons-lang3:3.20.+'
}

def mainClassName = 'net.anawesomguy.adventofcode.AdventOfCode'

jar {
    archiveClassifier = 'api'
    manifest.attributes([
            'Main-Class': mainClassName
    ])
}

sourceSets.forEach {
    if (!SourceSet.isMain(it) && it.name != SourceSet.TEST_SOURCE_SET_NAME) {
        def main = sourceSets.main
        dependencies.add(it.implementationConfigurationName, main.output)
        it.compileClasspath += main.compileClasspath
        it.compileClasspath += configurations.yearsImplementation
        it.runtimeClasspath += main.runtimeClasspath
        it.runtimeClasspath += configurations.yearsImplementation

        def jarTask = tasks.register(it.jarTaskName, Jar) { task ->
            group 'build'
            dependsOn it.compileJavaTaskName, it.processResourcesTaskName
            from it.output, main.output
            archiveClassifier = it.name
        }

        build.dependsOn jarTask

        tasks.register('run' + it.name.capitalize(), JavaExec) { task ->
            dependsOn jarTask
            classpath it.runtimeClasspath
            mainClass = mainClassName
            jvmArgs '-Xmx12M'
            workingDir 'build'
        }
    }
}